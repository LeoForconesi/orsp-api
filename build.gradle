
buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://oss.jfrog.org/repo/' }
        dependencies {
            classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0-SNAPSHOT'
        }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'org.broadinstitute.orsp.api.OrspApplication'

repositories {
    mavenLocal()
    mavenCentral()
    // Spock snapshots are available from the Sonatype OSS snapshot repository
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

// Project variables
project.ext {
    dropwizardVersion = '0.7.1'
    groovyVersion = '2.3.7'
    dropwizardAppConfig = 'src/main/resources/appconfig.yml'
    guiceVersion='3.0'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-hibernate:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-migrations:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-jackson:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-auth:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-client:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-assets:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-views:${dropwizardVersion}"
    compile "io.dropwizard:dropwizard-jdbi:${dropwizardVersion}"
    compile "org.slf4j:jul-to-slf4j:1.7.2"
    compile "org.slf4j:slf4j-api:1.7.2"
    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile "com.google.inject:guice:${guiceVersion}"

    // needed for validation tests so logging in hibernate validator does not throw exception
    compile 'ch.qos.logback:logback-classic:1.0.7'
    compile "org.slf4j:log4j-over-slf4j:1.7.2"

    compile "org.codehaus.gpars:gpars:1.2.1"
    compile "io.fastjson:boon:0.29"
    compile "net.vz.mongodb.jackson:mongo-jackson-mapper:1.4.2"
    compile 'javax.validation:validation-api:1.1.0.Final'

    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.0-SNAPSHOT"
    testCompile 'org.slf4j:log4j-over-slf4j:1.7.2'
    testRuntime "cglib:cglib-nodep:2.2.2"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:1.2"    // allows mocking of classes without default constructor (together with CGLIB)

}

run {
    args 'server', dropwizardAppConfig
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
        attributes 'BUILD_NUMBER': System.env.BUILD_NUMBER ?: 'LOCAL'
        attributes 'BUILD_URL': System.env.BUILD_URL ?: 'LOCAL'
        attributes 'GIT_COMMIT': System.env.GIT_COMMIT ?: 'LOCAL'
        attributes 'GIT_BRANCH': System.env.GIT_BRANCH ?: 'LOCAL'
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor-Id': project.group
    }
}

shadowJar {
    mergeServiceFiles()
}
